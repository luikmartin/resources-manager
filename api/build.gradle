plugins {
    id 'org.springframework.boot'
    id 'java'
}

dependencies {
    implementation(project(':common'),
            project(':core'),
            project(':liquibase'),

            libs.springBootStarterWeb,
            libs.springBootStarterActuator,
            libs.springBootStarterDataJpa,
            libs.springBootStarterKafka,
            libs.postgresql,
            libs.liquibaseCore,
            libs.springdocOpenapi)

    testImplementation(libs.springBootStarterTest,
            libs.testcontainersPostgresql,
            libs.testcontainersJunitJupiter,
            libs.testcontainersKafka,
            project(':common'),
            project(':core'))
}

tasks.named('test') {
    useJUnitPlatform {
        excludeTags 'integration'
    }

    exclude '**/*IntegrationTest*'
    exclude '**/*IT*'

    jvmArgs '-XX:+EnableDynamicAgentLoading'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests with Testcontainers PostgreSQL'
    group = 'verification'

    useJUnitPlatform {
        includeTags 'integration'
    }

    include '**/*IntegrationTest*'
    include '**/*IT*'

    mustRunAfter test

    jvmArgs '-XX:+UseG1GC', '-Xmx2g', '-XX:+EnableDynamicAgentLoading'
}

check.dependsOn integrationTest
